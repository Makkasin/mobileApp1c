&НаКлиенте
Перем КонстантаСклад;
&НаКлиенте
Перем СекундВПолеМесто;
&НаКлиенте
Перем КонтрольОткл;

&НаКлиенте
Процедура КонтрольОткл()
	Если КонтрольОткл=0 или КонтрольОткл=Неопределено Тогда Возврат; КонецЕсли;
	Рез = "ВНИМАНИЕ! ";
	Если КонтрольОткл<0 Тогда
		Рез = Рез + "недостача "+(-КонтрольОткл);
	Иначе
		Рез = Рез + "излишки "+КонтрольОткл;
	КонецЕсли;
	КонтрольОткл = Неопределено;
	
	#Если МобильноеПриложениеКлиент Тогда
		СредстваМультимедиа.ВоспроизвестиТекст(Рез); 
	#КонецЕсли
	
КонецПроцедуры

	

&НаСервереБезКонтекста
Функция ПолучитьИнвНом(Склад,Место,Номенклатура,ШтрихКод,Производитель,Кол)
	
	Обк = Справочники.инвИнвентаризация.СоздатьЭлемент();
	Обк.Дата = текущаяДата();
	Обк.Склад = Склад;
	Обк.Место = СокрЛП(Место);
	Обк.Номенклатура = Номенклатура;
	Обк.Код = номенклатура.Код;
	Обк.ШтрихКод = ШтрихКод;
	Обк.Производитель = Производитель;
	Если Кол=0 Тогда
		обк.Количество = 1;
	ИНаче
		обк.Количество = Кол;
	КонецЕсли;
	Обк.Лог = ""+ТекущаяДата()+"_"+ИмяКомпьютера();
	Обк.Записать();
	
	Возврат Обк.Ссылка;
	
КонецФункции

&НаСервереБезКонтекста
Функция НоменклатураСовпадает(Ном,ШтрихКод,ИнвИнв)
	Если ЗначениеЗаполнено(инвИнв)=Ложь Тогда Возврат Ложь; КонецЕСЛИ;
	Возврат Ном = инвИнв.Номенклатура и ШтрихКод = инвИнв.ШтрихКод;
	
КонецФункции

&НаКлиенте
Процедура НоменклатураОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	СтандартнаяОбработка = ложь;
	ТекНом = Неопределено;
	ШтрихКод = "";
	Производитель = "";
	
	//Установка места
	Если Лев(Текст,3) = "(!)" Тогда
		ЭтаФорма.Место = СокрЛП(Сред(Текст,4));
		#Если МобильноеПриложениеКлиент Тогда
			СредстваМультимедиа.ВоспроизвестиТекст("Место "+ЭтаФорма.Место); 
		#КонецЕсли
		ОбновитьМеста();
		Текст="";
	ИНачеЕсли  ЭтаФорма.Место="0" или СокрЛП(ЭтаФорма.Место)="" Тогда
		#Если МобильноеПриложениеКлиент Тогда
			СредстваМультимедиа.ВоспроизвестиТекст("Укажите место "); 
		#КонецЕсли
		Текст="";
	ИНаче
		ТекНом = глОбщий.НоменклатураАвтоПодборНаСервере(Текст,ШтрихКод,Производитель);
	КонецЕСЛИ;
	
	
	Если ТекНом <> Неопределено Тогда
		Сигнал();
		Текст = "";
		ЭтаФорма.СтрокаПодбора = "";
		Если НоменклатураСовпадает(ТекНом,ШтрихКод,ЭтаФорма.инвНоменклатура) Тогда
			КолПлюс(Элемент,Ложь);
		ИНаче
			КонтрольОткл();
			ЭтаФорма.Количество      = ПолучитьТхтОстатки(ТекНом,КонстантаСклад,КонтрольОткл,Истина);
			ЭтаФорма.инвНоменклатура = ПолучитьИнвНом(КонстантаСклад,ЭтаФорма.Место,ТекНом,ШтрихКод,Производитель,ЭтаФорма.Количество);
			Элементы.инвИнвентаризация.ТекущаяСтрока = ЭтаФорма.инвНоменклатура;
		КонецЕсли;
		
	ИНаче
		ЭтаФорма.СтрокаПодбора = Текст;
		Если СтрДлина(Текст)>7 Тогда
			ВопросШтрихКод();
		КонецеслИ;
		
		ЭтаФорма.Количество    = 0;
		фиксФакт = 0;
		ЭтаФорма.инвНоменклатура = Неопределено;
		КонтрольОткл();
	КонецеСЛИ;
		ОбновитьОтображениеДанныхФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросШтрихКод()
	
	Оповещение = Новый ОписаниеОповещения("ПослеОтветаНаВопрос",ЭтотОбъект); // Прописываем название процедуры-обработчика.
	
	
	
	ПоказатьВопрос(Оповещение, "Записать штрих-код?",  // вместо привычного "Вопрос", теперь "ПоказатьВопрос"
	
	РежимДиалогаВопрос.ДаНет,
	
	0,  // задержка (секунды). необязательно
	
	КодВозвратаДиалога.Да, // задает кнопку по умолчанию. необязательно
	
	"" // устанавливаем заголовок. необязательно
	
);
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтветаНаВопрос(Результат, Параметры) Экспорт // здесь, думаю, комментировать нечего


Если Результат = КодВозвратаДиалога.Да Тогда
	
	Фрм = ПолучитьФорму("РегистрСведений.ШтрихКоды.Форма.ФормаЗаписи",,ЭтаФорма,ЭтаФорма);
	Фрм.ЭтаФорма.Запись.ШтрихКод = ЭтаФорма.СтрокаПодбора;
	Фрм.Открыть();
	
КонецЕсли;

ЭтаФорма.СтрокаПодбора = "";

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗаголовокСОстатками()
	
	ТекКол = 0;
	ЭтаФорма.Заголовок = "Остаток: "+ПолучитьТхтОстатки(ЭтаФорма.инвНоменклатура,КонстантаСклад,КонтрольОткл,ложь,ТекКол);
	ИзменитьКоличествоТблМеста(ТекКол);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтображениеДанныхФормы()
	
	Элементы.Количество.ОбновитьТекстРедактирования();	
	Элементы.инвИнвентаризация.Обновить();
	ОбновитьЗаголовокСОстатками();
	//#Если МобильныйКлиент Тогда
		ЭтаФорма.ТекущийЭлемент = элементы.СтрокаПодбора;
		ЭтаФорма.НачатьРедактированиеЭлемента();
	//#КонецЕсли
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьТхтОстатки(текИнв,ТекСклад,КонтрольОткл,ВернутьЧисло=Ложь,фиксФакт=0)
	
	Если ЗначениеЗаполнено(текИнв) = Ложь Тогда
		Возврат "";
	КонецЕСЛИ;
	
	Запрос = новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	SUM(инвИнвентаризация.Количество) КАК Кол,
				   |    0 Ост,
				   |    0 КолИнв
	               |ИЗ
	               |	Справочник.инвИнвентаризация КАК инвИнвентаризация
	               |ГДЕ
	               |	инвИнвентаризация.Номенклатура = &Номенклатура
				   |
				   |UNION ALL
				   |
				   |ВЫБРАТЬ
				   |    0 Кол,
				   |	SUM(Остатки.Количество) КАК Ост,
				   |	SUM(Остатки.КолИнв) КАК КолИнв
				   |ИЗ
				   |	РегистрСведений.Остатки КАК Остатки
				   |ГДЕ
				   |	Остатки.Номенклатура = &Номенклатура
				   |  и Склад = &Склад
				   |";
	Если ТипЗнч(текИнв)=Тип("СправочникСсылка.Номенклатура") Тогда
		Запрос.УстановитьПараметр("Номенклатура",текИнв);
	Иначе
		Запрос.УстановитьПараметр("Номенклатура",текИнв.Номенклатура);
	КонецЕсли;
	Запрос.УстановитьПараметр("Склад",ТекСклад);
	Тбл = запрос.Выполнить().Выгрузить();
	
	фиксФакт = Тбл.Итог("Кол");
	
	//Конроль отклонений
	Если Тбл.Итог("Ост") = 0 Тогда
		КонтрольОткл = 0;
	Иначе
		КонтрольОткл = Тбл.Итог("Кол") - Тбл.Итог("Ост");
	КонецЕсли;
	
	Если ВернутьЧисло=Истина Тогда
		Если Тбл.Итог("Ост")=0 Тогда 
			Возврат 1;
		Иначе
			Возврат Тбл.Итог("Ост");
		КонецЕсли;
	Иначе
		Возврат ""+Тбл.Итог("Кол")+" / "+Тбл.Итог("Ост");
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура КолПлюс(Команда,ОбновитьДанныеФормы=Истина)
	
	Если ЗначениеЗаполнено(ЭтаФорма.инвНоменклатура)=Ложь Тогда ВозвраТ; КонецЕСЛИ;
	Если ЗначениеЗаполнено(ЭтаФорма.Место)=Ложь Тогда ВозвраТ; КонецЕСЛИ;
	
	Если Команда.Имя = "КолМинус" Тогда
		Кол =  -1;
	Иначе
		Кол = 1;
	КонецЕсЛИ;
	
	ИзменитьКоличество(Кол);
	
	Если ОбновитьДанныеФормы Тогда
		ОбновитьОтображениеДанныхФормы();
	КонецЕСЛИ;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьКоличество(Кол,установить=Ложь)
	ЭтаФорма.Количество = ИзменитьКоличествоНаСервере(этаФорма.инвНоменклатура,Кол,установить);
	#Если МобильноеПриложениеКлиент Тогда
		СредстваМультимедиа.ВоспроизвестиТекст(СокрЛП(ЭтаФорма.Количество)); 
	#КонецЕсли
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИзменитьКоличествоНаСервере(текИнв,Кол,установить=Ложь)
	
	Обк = текИнв.ссылка.ПолучитьОбъект();
	Если установить Тогда
		Обк.Количество = Макс(0,Кол);
	ИНаче
		Обк.Количество = Макс(0,Обк.Количество+Кол);
	КонецеслИ;
	Обк.Записать();
	
	Возврат Обк.Количество;
	
КонецФункции

&НаКлиенте
Процедура КоличествоПриИзменении(Элемент)
	Если ЗначениеЗаполнено(этаФорма.инвНоменклатура) Тогда
		ИзменитьКоличество(ЭтаФорма.Количество,истина);
		//Сигнал();
	ИНаче
		ЭтаФорма.Количество = 0;
	КонецЕСЛИ;
	ОбновитьОтображениеДанныхФормы();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	#Если МобильноеПриложениеКлиент Тогда
		ПодключитьОбработчикОжидания("ФокусНаПодбор",1,Ложь);
	#КонецЕсли
	
	СекундВПолеМесто = 0;

	КонстантаСклад = глДоступ.ПолучитьОсновнойСклад();
	Если ЗначениеЗаполнено(КонстантаСклад) = Ложь ТОгда
		отказ = истина;
		Сообщить("Не выбран склад!");
	КонецЕСЛИ;
	
КонецПроцедуры

&НаКлиенте
Процедура ФокусНаПодбор()
	
	Если ТипЗнч(ЭтаФорма.ТекущийЭлемент)=Тип("ПолеФормы") Тогда
		Простой = ложЬ;
		Если ЭтаФорма.ТекущийЭлемент.Имя = "Место" и СекундВПолеМесто<=3  Тогда
			СекундВПолеМесто = СекундВПолеМесто + 1;
			Простой = Истина;
		ИНаче
			СекундВПолеМесто = 0;
		КонецЕСЛИ;
		
		Если ЭтаФорма.ТекущийЭлемент.Имя = "СтрокаПодбора" Тогда
			ЭтаФорма.НачатьРедактированиеЭлемента();
			Возврат;
		ИНачеЕсли ЭтаФорма.ТекущийЭлемент.Имя = "Количество" 
			или Простой
			//		  или ЭтаФорма.ТекущийЭлемент.Имя = "Место" 
			Тогда
			Возврат;
		КонецЕСЛИ;
	КонецЕСЛИ;
	
	ЭтаФорма.ТекущийЭлемент = элементы.СтрокаПодбора;
	ЭтаФорма.НачатьРедактированиеЭлемента();
	
КонецПроцедуры


&НаКлиенте
Процедура ОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	ЭтаФорма.СтрокаПодбора = "";
	ЭтаФорма.Количество    = 0;
	ЭтаФорма.инвНоменклатура = Неопределено;
	фиксФакт = 0;
	КонтрольОткл();
	//Сообщить(НовыйОбъект);
	Элементы.инвИнвентаризация.ТекущаяСтрока = НовыйОбъект;
	ОбновитьОтображениеДанныхФормы();
КонецПроцедуры

&НаКлиенте
Процедура инвИнвентаризацияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = ложь;
	ОткрытьФорму("Справочник.инвИнвентаризация.ФормаОбъекта",Новый Структура("Ключ",ВыбраннаяСтрока),ЭтаФорма,ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура МестоОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка) 
	
	ЭтаФорма.Количество    = 0;
	ЭтаФорма.инвНоменклатура = Неопределено;
	фиксФакт = 0;
	КонтрольОткл();
	ОбновитьОтображениеДанныхФормы();
	ОбновитьМеста();
	
КонецПроцедуры

&НаКлиенте
Процедура Сверка(Команда)
	Стк = Новый Структура("Склад",КонстантаСклад);
	ОткрытьФорму("Обработка.Инвентаризация.Форма.ФормаСверки",стк,ЭтаФорма,,,,,);
КонецПроцедуры


&НаКлиенте
Процедура СканироватьШтрихКод(Команда)
	
	#Если МобильноеПриложениеКлиент Тогда
		Опопвещение = Новый ОписаниеОповещения("ЗавершениеСканирования",ЭтотОбъект);
		СредстваМультимедиа.ПоказатьСканированиеШтрихКодов("Сканирование штрих-кода",Опопвещение);
		
		//бин = СредстваМультимедиа.СделатьФотоснимок(ТипКамерыУстройства.Задняя);
		//Сообщить(Бин.ТипСодержимого);
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеСканирования(ШтрихКод,Результат,Сообщение,ДопПар ) Экспорт
	
	НоменклатураОкончаниеВводаТекста(Элементы.СтрокаПодбора, ШтрихКод, Неопределено, Неопределено, ложь);	
	#Если МобильноеПриложениеКлиент Тогда
		СредстваМультимедиа.ЗакрытьСканированиеШтрихКодов();
	#КонецЕсли

	
КонецПроцедуры


#Область ТблМЕста
&НаКлиенте
Процедура ОбновитьМеста(Команда=Неопределено,ТекущаяСтраница=Неопределено)
	
	Если Элементы.ГрСтраницы.ТекущаяСтраница = Элементы.стМеста Тогда 
		
		тблМеста.Очистить();
		Мас = ДанныеОстатки(КонстантаСклад,Место,Неопределено);
		
		Для каждого Стк из Мас Цикл
			НовСтр = тблМеста.Добавить();
			ЗаполнитьЗначенияСвойств(новСтр,Стк);
		КонецЦикла;
		
	ИначеЕсли Элементы.ГрСтраницы.ТекущаяСтраница = Элементы.стНомен Тогда 
		
		Если Элементы.инвИнвентаризация.ТекущиеДанные = Неопределено Тогда
			Если ЗначениеЗаполнено(инвНоменклатура) Тогда
				текНом = ПолучитьНоменклатуруИзИнвНоменклатуры(инвНоменклатура);
			ИНаче
				Возврат;
			КонецЕсли;
		ИНаче
			текНом = Элементы.инвИнвентаризация.ТекущиеДанные.Номенклатура;
		КонецЕСлИ;
		
		тблНом.Очистить();
		Мас = ДанныеОстатки(КонстантаСклад,Неопределено,текНом);
		
		Для каждого Стк из Мас Цикл
			НовСтр = тблНом.Добавить();
			ЗаполнитьЗначенияСвойств(новСтр,Стк);
		КонецЦикла;

		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьНоменклатуруИзИнвНоменклатуры(инвНоменклатура)
	Возврат инвНоменклатура.Номенклатура;
КонецФункции

&НаКлиенте
Процедура ИзменитьКоличествоТблМеста(ТекКол)
	
	ном = ПолучитьНоменклатуруИзИнвНоменклатуры(инвНоменклатура);
	Для каждого Стр из тблМеста Цикл
		Стр.послСтрока = ложЬ;
		Если Стр.Номенклатура = Ном ТОгда
			ТекСтр = Стр;
		КонецЕсли;
	КонецЦикла;
	
	Если текСтр = Неопределено ТОгда
		текСтр = тблМЕста.Добавить();
		текстр.Номенклатура = ном;
	КонецЕСлИ;
	
	ТекСтр.Факт = ТекКол;
	текстр.послСтрока = Истина;
	
	тблМЕста.Сдвинуть(тблМЕста.Индекс(текстр), -тблМЕста.Индекс(текстр));
	Элементы.тблМеста.ТекущаяСтрока = текстр.получитьИдентификатор();
	
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеОстатки(ТекСклад,текМесто,текНом)
	
	Запрос = новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
				   | Номенклатура,
				   | место,
				   | SUM(факт) факт,
				   | SUM(Учет) Учет,
				   | SUM(Другой) Другой
				   |
				   |FROM
				   |(
				   |ВЫБРАТЬ
				   |    Номенклатура,
				   |    Место,
	               |	инвИнвентаризация.Количество КАК Факт,
				   |    0 Учет,
				   |    0 Другой
	               |ИЗ
	               |	Справочник.инвИнвентаризация КАК инвИнвентаризация
	               |ГДЕ
	               |	(инвИнвентаризация.Место = &Место or &Место = Неопределено)
	               |  и	(инвИнвентаризация.Номенклатура = &Номенклатура or &Номенклатура = Неопределено)
				   |  и Склад = &Склад
				   |
				   |UNION ALL
				   |
				   |ВЫБРАТЬ
				   |    Номенклатура,
				   |    Место,
				   |    0 Факт,
				   |	Остатки.Количество КАК Учет,
				   |	Остатки.КолИнв КАК Другой
				   |ИЗ
				   |	РегистрСведений.Остатки КАК Остатки
				   |ГДЕ
				   |	(Остатки.Место = &Место or &Место = Неопределено)
				   |  и	(Остатки.Номенклатура = &Номенклатура or &Номенклатура = Неопределено)
				   |  и Склад = &Склад
				   |) ТБл
				   |GROUP BY Номенклатура, место
				   |ORDER BY Номенклатура.Наименование
				   |";
	Запрос.УстановитьПараметр("Место",текМесто);
	Запрос.УстановитьПараметр("Номенклатура",текНом);
	Запрос.УстановитьПараметр("Склад",ТекСклад);
	Тбл = запрос.Выполнить().Выгрузить();
	
	Мас = Новый Массив;
	
	Для каждого Стр из Тбл Цикл
		Стк = Новый Структура("Номенклатура,место,факт,учет,Другой");
		ЗаполнитьЗначенияСвойств(Стк,Стр);
		МАс.Добавить(Стк);
	Конеццикла;
	
	
	Возврат Мас;
	
КонецФункции

#КонецОбласти
