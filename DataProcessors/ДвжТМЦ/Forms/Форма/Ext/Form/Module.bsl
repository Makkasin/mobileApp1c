&НаКлиенте
Перем КонстантаСклад;
&НаКлиенте
Перем СекундВПолеМесто;

&НаСервере
Функция ПолучитьИнвНом(КонстантаСклад,Номенклатура,ШтрихКод,Производитель,ТекКоличество=1,ЕстьОШибка=Ложь)
	
	ЗаявкаНаВыдачу = Неопределено;
	Если КонстантаСклад.ВыдчаТМЦчерезСогласование=Истина Тогда
		ЗаявкаНаВыдачу = Справочники.ЗаявкиНаВыдачуТМЦ.ПроверкаВыдачи(Номенклатура,гарНомер,ДокументОснование,ТекКоличество);
		Если ЗаявкаНаВыдачу=ЛОжь Тогда 
			ЕстьОшибка = Истина;
			Возврат Неопределено; 
		КонецЕсли;
	КонецЕсли;
	
	Обк = Справочники.двжТМЦ.СоздатьЭлемент();
	Обк.Дата = текущаяДата();
	Обк.Склад = КонстантаСклад;
	Обк.Место = Место;
	Обк.Номенклатура = Номенклатура;
	Обк.Код = номенклатура.Код;
	Обк.ШтрихКод = ШтрихКод;
	Обк.Производитель = Производитель;
	обк.Количество = ТекКоличество;
	Обк.Лог = ""+ТекущаяДата()+"_"+ИмяКомпьютера();
	Обк.ГарНомер = гарНомер; 
	Обк.ГосНомер = госНомер; 
	Обк.ТС = ТС; 
	Обк.ТабНомер = ТабНомер; 
	Обк.Сотрудник = Сотрудник; 
	Обк.ДокументОснование = ДокументОснование; 
	Обк.ВидДвижения = 0;
	Обк.ИННДО = ИННДО;
	Обк.ЗаявкаНаВыдачуТМЦ = ЗаявкаНаВыдачу;
	Обк.Записать();
	
	Возврат Обк.Ссылка;
	
КонецФункции

&НаСервереБезКонтекста
Функция НоменклатураСовпадает(Ном,ШтрихКод,ИнвИнв,ГарНомер)
	Если ЗначениеЗаполнено(инвИнв)=Ложь Тогда Возврат Ложь; КонецЕСЛИ;
	Возврат Ном = инвИнв.Номенклатура и ШтрихКод = инвИнв.ШтрихКод и ГарНомер = инвИнв.ГарНомер;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДО(ИНН,КонстантаСклад)
	сс = Справочники.Организации.НайтиПоКоду(ИНН);
	Если сс.Пустая() Тогда
		Возврат Неопределено;
	КонецЕсли;
	Если сс = КонстантаСклад.Организация Тогда
		Возврат Справочники.Организации.ПустаяСсылка();
	ИНаче
		Возврат сс;
	КонецеСЛИ;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьОснование(ДанныЕ)
	
	СткРез = глОбщийКлиент.ЗаполнитьОснование(ДанныЕ);
	
	ДокументОснование = СткРез.ВидДок+" №"+СткРез.номерПЛ+" от "+Формат(СткРез.ДатаПЛ,"ДФ=dd.MM.yyyy");
	
	гарНомер 	= СткРез.ГарНомер;
	ГосНомер 	= СткРез.ГосНомер;
	ТС			= СткРез.ТС;
	Сотрудник 	= СткРез.Сотрудник;
	ТабНомер	= СткРез.ТабНомер;
	Если СокрЛП(СткРез.ИНН)<>"" Тогда
		организацияДО = ПолучитьДО(СткРез.ИНН,КонстантаСклад);
		ИННДО  = СткРез.ИНН;
	ИНаче
		организацияДО = "";
		ИННДО  = "";
	КонецЕсли;
	Элементы.организацияДО.Видимость = ЗначениеЗаполнено(организацияДО);
	
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураОкончаниеВводаТекста(Элемент, Текст="", ДанныеВыбора=Неопределено, ПараметрыПолученияДанных=Неопределено, СтандартнаяОбработка=Истина)
	СтандартнаяОбработка = ложь;
	ТекНом = Неопределено;
	ШтрихКод = "";
	Производитель = "";
	ТекКоличество = 1;
	
	Если ТипЗнч(Элемент)=Тип("Структура") Тогда
		ТекНом = Элемент.Номенклатура;
		ЭтаФорма.инвНоменклатура = Неопределено;
		ТекКоличество = Элемент.Количество;
		Место =  Элемент.Место;
		
	//Установка места
	ИНачеЕсли Лев(Текст,3) = "(!)" Тогда
		ЭтаФорма.Место = Сред(Текст,4);
		#Если МобильноеПриложениеКлиент Тогда
			СредстваМультимедиа.ВоспроизвестиТекст("Место "+ЭтаФорма.Место); 
		#КонецЕсли
		Текст="";
	ИНачеЕсли стрДлина(Текст)>21  Тогда
		ЗаполнитьОснование(Текст);
		Текст="";
	ИначеЕсли СокрлП(ДокументОснование)="" ТОгда
		
		#Если МобильноеПриложениеКлиент Тогда
			СредстваМультимедиа.ВоспроизвестиТекст("Не указан документ основание!"); 
		#КонецЕсли
		Текст="";
	//ИНачеЕсли  ЭтаФорма.Место=0 Тогда
	//	#Если МобильноеПриложениеКлиент Тогда
	//		СредстваМультимедиа.ВоспроизвестиТекст("Укажите место "); 
	//	#КонецЕсли
	//	Текст="";
	ИНаче
		ТекНом = глОбщий.НоменклатураАвтоПодборНаСервере(Текст,ШтрихКод,Производитель);
	КонецЕСЛИ;
	
	
	Если ТекНом <> Неопределено Тогда
		Текст = "";
		ЭтаФорма.СтрокаПодбора = "";
		ЕстьОшибка = Ложь;
		Если НоменклатураСовпадает(ТекНом,ШтрихКод,ЭтаФорма.инвНоменклатура,ГарНомер) Тогда
			КолПлюс(Элемент,Ложь,ЕстьОшибка);
		ИНаче
			ЭтаФорма.Количество      = ТекКоличество;
			ЭтаФорма.инвНоменклатура = ПолучитьИнвНом(КонстантаСклад,ТекНом,ШтрихКод,Производитель,ТекКоличество,ЕстьОшибка);
			Элементы.инвИнвентаризация.ТекущаяСтрока = ЭтаФорма.инвНоменклатура;
		КонецЕсли;
		Если ЕстьОшибка=Истина Тогда
			СообщитьОщибкаСогласования();
		Иначе
			Сигнал();
		КонецЕсли;
		
	ИНаче
		ЭтаФорма.СтрокаПодбора = Текст;
		//Если СтрДлина(Текст)>7 Тогда
		//	ВопросШтрихКод();
		//КонецеслИ;
		
		ЭтаФорма.Количество    = 0;
		ЭтаФорма.инвНоменклатура = Неопределено;
	КонецеСЛИ;
	
	ОбновитьОтображениеДанныхФормы();
	
КонецПроцедуры
	

&НаКлиенте
Процедура ПодборПоКодуОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	НоменклатураОкончаниеВводаТекста(Элемент, "!!"+СокрЛП(Текст), ДанныеВыбора, ПараметрыПолученияДанных, ложь);
КонецПроцедуры

&НаКлиенте
Процедура ВопросШтрихКод()
	
	Оповещение = Новый ОписаниеОповещения("ПослеОтветаНаВопрос",ЭтотОбъект); // Прописываем название процедуры-обработчика.
	
	
	
	ПоказатьВопрос(Оповещение, "Записать штрих-код?",  // вместо привычного "Вопрос", теперь "ПоказатьВопрос"
	
	РежимДиалогаВопрос.ДаНет,
	
	0,  // задержка (секунды). необязательно
	
	КодВозвратаДиалога.Да, // задает кнопку по умолчанию. необязательно
	
	"" // устанавливаем заголовок. необязательно
	
);
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтветаНаВопрос(Результат, Параметры) Экспорт // здесь, думаю, комментировать нечего


Если Результат = КодВозвратаДиалога.Да Тогда
	
	Фрм = ПолучитьФорму("РегистрСведений.ШтрихКоды.Форма.ФормаЗаписи",,ЭтаФорма,ЭтаФорма);
	Фрм.ЭтаФорма.Запись.ШтрихКод = ЭтаФорма.СтрокаПодбора;
	Фрм.Открыть();
	
КонецЕсли;

ЭтаФорма.СтрокаПодбора = "";

КонецПроцедуры


&НаКлиенте
Процедура ОбновитьОтображениеДанныхФормы()
	
	Элементы.Количество.ОбновитьТекстРедактирования();	
	Элементы.инвИнвентаризация.Обновить();
	ЭтаФорма.Заголовок = "Остаток: "+ПолучитьТхтОстатки(ЭтаФорма.инвНоменклатура,КонстантаСклад,ЭтаФорма.Количество);
	
	//#Если МобильныйКлиент Тогда
		ЭтаФорма.ТекущийЭлемент = элементы.СтрокаПодбора;
		ЭтаФорма.НачатьРедактированиеЭлемента();
	//#КонецЕсли
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьТхтОстатки(текИнв,ТекСклад,ТекКол)
	
	Если ЗначениеЗаполнено(текИнв) = Ложь Тогда
		Возврат "";
	КонецЕСЛИ;
	
	Стк = РегистрыСведений.Остатки.ПолучитьОстатокПоНоменклатуре(ТекСклад,текИнв.Номенклатура);
	
	Возврат ""+ТекКол+"/"+(Стк.Остаток);//+ТекКол);
	
	
КонецФункции

&НаКлиенте
Процедура СообщитьОщибкаСогласования()
	#Если МобильноеПриложениеКлиент Тогда
		СредстваМультимедиа.ВоспроизвестиТекст("Ошибка согласования"); 
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура КолПлюс(Команда,ОбновитьДанныеФормы=Истина,ЕстьОшибка=ЛОжь)
	
	Если ЗначениеЗаполнено(ЭтаФорма.инвНоменклатура)=Ложь Тогда ВозвраТ; КонецЕСЛИ;
	
	Если Команда.Имя = "КолМинус" Тогда
		Кол =  -1;
	Иначе
		Кол = 1;
	КонецЕсЛИ;
	
	ЭтаФорма.Количество = ИзменитьКоличество(этаФорма.инвНоменклатура,Кол,ЕстьОшибка);
	#Если МобильноеПриложениеКлиент Тогда
		Если ЕстьОШибка=Истина Тогда
		СообщитьОщибкаСогласования();	
		ИНаче
		СредстваМультимедиа.ВоспроизвестиТекст(СокрЛП(ЭтаФорма.Количество)); 
		КонецЕсли;
	#КонецЕсли
	//Сигнал();
	Если ОбновитьДанныеФормы Тогда
		ОбновитьОтображениеДанныхФормы();
	КонецЕСЛИ;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИзменитьКоличество(текИнв,Кол,ЕстьОшибка,установить=Ложь)
	
	Обк = текИнв.ПолучитьОбъект();
	Если Обк.Склад.ВыдчаТМЦчерезСогласование Тогда
		Если Установить=истина ТОгда
			пКол = -Обк.Количество+Кол;
		Иначе
			пКол = кол;
		КонецЕсли;
		Если Справочники.ЗаявкиНаВыдачуТМЦ.ПроверкаВыдачиСсылка(Обк.ЗаявкаНаВыдачуТМЦ,пКол)=Ложь Тогда
			ЕстьОшибка = Истина;
			Возврат Обк.Количество;
		КонецЕсли;
	КонецЕсли;
	
	Если установить Тогда
		Обк.Количество = Макс(0,Кол);
	ИНаче
		Обк.Количество = Макс(0,Обк.Количество+Кол);
	КонецеслИ;
	Обк.Записать();
	
	Возврат Обк.Количество;
	
КонецФункции

&НаКлиенте
Процедура КоличествоПриИзменении(Элемент)
	Если ЗначениеЗаполнено(этаФорма.инвНоменклатура) Тогда
		ЕстьОшибка = ЛОжь;
		ЭтаФорма.Количество = ИзменитьКоличество(этаФорма.инвНоменклатура,ЭтаФорма.Количество,ЕстьОшибка,истина);
		Если ЕстьОшибка=Истина Тогда
			СообщитьОщибкаСогласования();
		ИНаче
			Сигнал();
		КонецЕсли;
	ИНаче
		ЭтаФорма.Количество = 0;
	КонецЕСЛИ;
	ОбновитьОтображениеДанныхФормы();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	#Если МобильноеПриложениеКлиент Тогда
		ПодключитьОбработчикОжидания("ФокусНаПодбор",1,Ложь);
	#КонецЕсли
	
	СекундВПолеМесто = 0;
	ДатаОтчета = ТекущаяДата();

	КонстантаСклад = глДоступ.ПолучитьОсновнойСклад();
	Если ЗначениеЗаполнено(КонстантаСклад) = Ложь ТОгда
		отказ = истина;
		Сообщить("Не выбран склад!");
	КонецЕСЛИ;
	
КонецПроцедуры


&НаКлиенте
Процедура ФокусНаПодбор()
	
	Если  Элементы.грСтраницы.ТекущаяСтраница <> Элементы.ГрСтр1 ТОгда Возврат; КонецеСЛИ;
	
	Если ТипЗнч(ЭтаФорма.ТекущийЭлемент)=Тип("ПолеФормы") Тогда
		Простой = ложЬ;
		Если ЭтаФорма.ТекущийЭлемент.Имя = "Место" и СекундВПолеМесто<=3  Тогда
			СекундВПолеМесто = СекундВПолеМесто + 1;
			Простой = Истина;
		ИНаче
			СекундВПолеМесто = 0;
		КонецЕСЛИ;
		
		Если ЭтаФорма.ТекущийЭлемент.Имя = "СтрокаПодбора" Тогда
			ЭтаФорма.НачатьРедактированиеЭлемента();
			Возврат;
		ИНачеЕсли ЭтаФорма.ТекущийЭлемент.Имя = "Количество" 
			или   ЭтаФорма.ТекущийЭлемент.Имя = "ПодборПоКоду"
			или   Простой
			//		  или ЭтаФорма.ТекущийЭлемент.Имя = "Место" 
			Тогда
			Возврат;
		КонецЕСЛИ;
	КонецЕСЛИ;
	
	ЭтаФорма.ТекущийЭлемент = элементы.СтрокаПодбора;
	ЭтаФорма.НачатьРедактированиеЭлемента();
	
КонецПроцедуры


&НаКлиенте
Процедура ОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	ЭтаФорма.СтрокаПодбора = "";
	ЭтаФорма.Количество    = 0;
	ЭтаФорма.инвНоменклатура = Неопределено;
	//Сообщить(НовыйОбъект);
	Элементы.инвИнвентаризация.ТекущаяСтрока = НовыйОбъект;
	ОбновитьОтображениеДанныхФормы();
КонецПроцедуры

&НаКлиенте
Процедура инвИнвентаризацияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = ложь;
	ОткрытьФорму("Справочник.двжТМЦ.ФормаОбъекта",Новый Структура("Ключ",ВыбраннаяСтрока),ЭтаФорма,ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура МестоОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
		ЭтаФорма.Количество    = 0;
		ЭтаФорма.инвНоменклатура = Неопределено;
		ОбновитьОтображениеДанныхФормы();
КонецПроцедуры

&НаКлиенте
Процедура СканироватьШтрихКод(Команда)
	
	#Если МобильноеПриложениеКлиент Тогда
		Опопвещение = Новый ОписаниеОповещения("ЗавершениеСканирования",ЭтотОбъект);
		СредстваМультимедиа.ПоказатьСканированиеШтрихКодов("Сканирование штрих-кода",Опопвещение);
		
		//бин = СредстваМультимедиа.СделатьФотоснимок(ТипКамерыУстройства.Задняя);
		//Сообщить(Бин.ТипСодержимого);
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеСканирования(ШтрихКод,Результат,Сообщение,ДопПар ) Экспорт
	
	НоменклатураОкончаниеВводаТекста(Элементы.СтрокаПодбора, ШтрихКод, Неопределено, Неопределено, ложь);	
	#Если МобильноеПриложениеКлиент Тогда
		СредстваМультимедиа.ЗакрытьСканированиеШтрихКодов();
	#КонецЕсли

	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьДень(Команда)
	
	ТекДт = глОбщий.ПоследнийНезакратыйДень();
	Если ЗначениеЗаполнено(ТекДТ)=Ложь ТОгда
		Сообщить("Все транзакции закрыты!");
		Возврат;
	КонецеСЛИ;
	
	Пока ТекДт < ТекущаяДата() Цикл
		глВыгрузкаДанных.ВыгрузитьДвжТМЦ(Истина);
		ТекДт = КонецДня(ТекДт)+1;
	КонецЦикла;
	Оповестить("ОбновитьДтСинхро");
	ЭтаФорма.Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура грСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	Если ТекущаяСтраница = Элементы.ГрСтр2 ТОгда
		ОбновитьНаСервере(КонстантаСклад);
	КонецеСЛИ;
КонецПроцедуры


&НаСервере
Процедура ОбновитьНаСервере(КонстантаСклад)
	
	тДрево = РеквизитФормыВЗначение("Древо");
	тДрево.Строки.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	двжТМЦ.Номенклатура КАК Номенклатура,
	               |	двжТМЦ.ГарНомер КАК ГарНомер,
	               |	двжТМЦ.ТС КАК ТС,
	               |	двжТМЦ.Сотрудник КАК Сотрудник,
	               |	двжТМЦ.Количество КАК Количество,
	               |	двжТМЦ.Номенклатура.Код КАК Код
	               |ИЗ
	               |	Справочник.двжТМЦ КАК двжТМЦ
	               |ГДЕ
	               |	двжТМЦ.Склад = &Склад
	               |	И двжТМЦ.Дата >= &Дата1
	               |	И двжТМЦ.Дата < &Дата2
	               |УПОРЯДОЧИТЬ ПО
	               |	Номенклатура.наименование
	               |ИТОГИ
	               |	СУММА(Количество)
	               |ПО
	               |	Номенклатура";
	
	Запрос.УстановитьПараметр("Дата1",НачалоДня(ДатаОтчета));
	Запрос.УстановитьПараметр("Дата2",КонецДня(ДатаОтчета)+1);
	Запрос.УстановитьПараметр("Склад",КонстантаСклад);
	
	
	Выб = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока Выб.Следующий() Цикл
		ТекСтроки = тДрево.строки.Добавить();
		ЗаполнитьЗначенияСвойств(ТекСтроки,Выб);
		ТекСтроки.Сотрудник = "";
		
		пВыб = Выб.Выбрать();
		Пока пВыб.Следующий() Цикл
			новСтр = ТекСтроки.строки.Добавить();
			ЗаполнитьЗначенияСвойств(новСтр,пВыб,,"Номенклатура,Код");
			НовСтр.Номенклатура = Лев(пВыб.ТС,30)+" - "+пВыб.Сотрудник;
			НоВстр.Сотрудник = пВыб.Сотрудник;
			НовСтр.Код = пВЫб.ГарНомер;
		Конеццикла;
		
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(тДрево, "Древо");
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОтчетаПриИзменении(Элемент)
	ОбновитьНаСервере(КонстантаСклад);
КонецПроцедуры

&НаКлиенте
Процедура Подбор(Команда)
	//ДокументОснование = "тест";
	Если СокрлП(ДокументОснование)="" ТОгда
		#Если МобильноеПриложениеКлиент Тогда
			СредстваМультимедиа.ВоспроизвестиТекст("Не указан документ основание!"); 
		#КонецЕсли
		Возврат;
	КонецЕСЛИ;
	
	Стк = Новый Структура("Склад",КонстантаСклад);
	ОткрытьФорму("Обработка.Подбор.Форма.Форма",стк,ЭтаФорма,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	//Вставить содержимое обработчика
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаПодбора(Номенклатура,Место,Количество) Экспорт
	НоменклатураОкончаниеВводаТекста(Новый Структура("Номенклатура,Место,Количество",Номенклатура,Место,Количество));
КонецПроцедуры

&НаКлиенте
Процедура СверкаСБазой(Команда)
	ОткрытьФорму("Справочник.двжТМЦ.Форма.ФормаСверка",,ЭтаФорма,ЭтаФорма);
КонецПроцедуры







